apply plugin: 'eclipse'
apply plugin: 'java'

version = '0.7.4'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }

repositories {
	jcenter()
}

dependencies {
	compile fileTree(dir: 'lib',
		includes: ['**/*.jar'],
		excludes: ['**/*-sources.jar', '**/*-javadoc.jar'])
		
	compile 'com.vladsch.flexmark:flexmark:0.50.40'
	compile 'com.vladsch.flexmark:flexmark-ext-attributes:0.50.40'
	compile 'com.vladsch.flexmark:flexmark-ext-definition:0.50.40'
	compile 'com.vladsch.flexmark:flexmark-ext-tables:0.50.40'
	compile 'com.vladsch.flexmark:flexmark-ext-typographic:0.50.40'
	compile 'com.vladsch.flexmark:flexmark-ext-wikilink:0.50.40'
	compile 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:0.50.40'
	compile 'com.vladsch.flexmark:flexmark-ext-gfm-tasklist:0.50.40'
	
	compile 'com.github.sommeri:less4j:1.17.2'
	compile 'org.freemarker:freemarker:2.3.29'
	compile 'org.nanohttpd:nanohttpd:2.3.1'
	
	compile 'org.kordamp.ikonli:ikonli-javafx:11.1.0'
	compile 'org.kordamp.ikonli:ikonli-materialdesign-pack:11.1.0'
	
	compile 'com.github.luccappellaro:jzopfli:0.0.4'

	compile 'net.osdn.blogs.flexmark.ext:flexmark-ext-highlight:0.2.3'
	compile 'net.osdn.blogs.flexmark.ext:flexmark-ext-inline-tags:0.1.4'
	compile 'net.osdn.util.io:auto-detect-reader:0.1.1'
}

defaultTasks 'clean', 'build', 'exewrap'

jar {
	manifest {
		attributes "Specification-Version": "${project.version}"
		attributes "Main-Class": "net.osdn.catalpa.ui.javafx.Main"
		attributes "Splashscreen-Image": "img/splash.gif"
		attributes "Class-Path": configurations.runtime.collect { "lib/$it.name" }.join(' ')
	}
	from('src/main/java') {
		include '**/*.fxml'
		include '**/*.css'
	}
	exclude('img/*.psd', 'ico/**', 'psd/**')
}


task copyLibraries(type: Copy) {
	setGroup("build")
	setDescription("Copy dependency libraries.")
	from (sourceSets.main.runtimeClasspath) {
		include '*.jar'
	}
	from('lib') {
		include '*.dll'
	}
	into "${buildDir}/package/lib/"
}

task createJavaRuntime(type: Exec, dependsOn: ['jar', 'copyLibraries']) {
	setGroup("build")
	setDescription("Create Java Runtime.")
	workingDir "${buildDir}/package/"
	commandLine 'cmd', '/c', 'CreateJRE.bat', "${jar.archiveFile.get()}", "lib"
}

task exewrap(type: Exec, dependsOn: 'jar') {
	setGroup("build")
	setDescription('Assembles the executable.')
	executable "${projectDir}/exewrap.exe"
	args "-A", "x64",
		"-g",
		"-t", "11",
		"-a", "-XX:+TieredCompilation -XX:TieredStopAtLevel=1 -XX:+UseSerialGC -XX:CICompilerCount=1 -Xverify:none -Xms128m",
		"-L", "lib",
		"-e", "SHARED",
		"-j", "${jar.archiveFile.get()}",
		"-i", "src/main/resources/ico/app.ico",
		"-o", "${buildDir}/package/${archivesBaseName}.exe",
		"-d", "Catalpa",
		"-p", "Catalpa",
		"-c", "(C) 2019 HIRUKAWA Ryo",
		"-V", "${version}",
		"-v", "${version}"
}
