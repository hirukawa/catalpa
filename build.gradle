apply plugin: 'java'

version = '0.8.8'

tasks.withType(JavaCompile) {
	sourceCompatibility = 17
	targetCompatibility = 17
	options.compilerArgs << '-Xlint:all'
}

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }

sourceSets.main.resources {
	srcDirs = ['src/main/resources', 'src/main/java' ]
}

repositories {
	mavenCentral()
}

dependencies {
	implementation fileTree(dir: 'lib',
		includes: ['**/*.jar'],
		excludes: ['**/*-sources.jar', '**/*-javadoc.jar'])

	implementation 'com.vladsch.flexmark:flexmark:0.62.2'
	implementation 'com.vladsch.flexmark:flexmark-ext-attributes:0.62.2'
	implementation 'com.vladsch.flexmark:flexmark-ext-definition:0.62.2'
	implementation 'com.vladsch.flexmark:flexmark-ext-tables:0.62.2'
	implementation 'com.vladsch.flexmark:flexmark-ext-typographic:0.62.2'
	implementation 'com.vladsch.flexmark:flexmark-ext-wikilink:0.62.2'
	implementation 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:0.62.2'
	implementation 'com.vladsch.flexmark:flexmark-ext-gfm-tasklist:0.62.2'

	implementation 'com.github.sommeri:less4j:1.17.2'
	implementation 'org.freemarker:freemarker:2.3.31'
	implementation 'org.nanohttpd:nanohttpd:2.3.1'
	implementation 'net.java.dev.jna:jna:5.6.0'
	implementation 'org.kordamp.ikonli:ikonli-javafx:12.2.0'
	implementation 'org.kordamp.ikonli:ikonli-materialdesign-pack:12.2.0'
	implementation 'com.github.luccappellaro:jzopfli:0.0.4'
	implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.15'

	// jcenter が廃止になったのでプロジェクト lib に配置変更しました。
	// implementation 'net.osdn.blogs.flexmark.ext:flexmark-ext-highlight:0.3.1'
	// implementation 'net.osdn.blogs.flexmark.ext:flexmark-ext-inline-tags:0.3.1'
	// implementation 'net.osdn.util.io:auto-detect-reader:0.1.1'
	// implementation 'net.osdn.util.rest.client:rest-client-util:0.3.2'

	// rest-client-util で必要です。
	implementation 'com.squareup.okhttp3:okhttp:3.14.9'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.12.5'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.5'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.5'

	// auto-detect-reader で必要です。
	implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
}

defaultTasks 'clean', 'build', 'exewrap'

jar {
	manifest {
		attributes "Specification-Version": "${project.version}"
		attributes "Main-Class": "net.osdn.catalpa.ui.javafx.Main"
		attributes "Splashscreen-Image": "img/splash.gif"
		attributes "Class-Path": sourceSets.main.runtimeClasspath.collect { "lib/$it.name" }.join(' ')
	}
	into('net/osdn/catalpa/ui/javafx') {
		from("${projectDir}") {
			include('LICENSE.txt')
		}
	}
}


task copyLibraries(type: Copy) {
	setGroup("build")
	setDescription("Copy dependency libraries.")
	from (sourceSets.main.runtimeClasspath) {
		include '*.jar'
	}
	from('lib') {
		include '*.dll'
	}
	into "${buildDir}/package/lib/"
}

task createJavaRuntime(type: Exec, dependsOn: ['jar', 'copyLibraries']) {
	setGroup("build")
	setDescription("Create Java Runtime.")
	workingDir "${buildDir}/package/"
	commandLine 'cmd', '/c', 'CreateJRE.bat', "${jar.archiveFile.get()}", "lib", "-client"
}

task exewrap(type: Exec, dependsOn: 'jar') {
	setGroup("build")
	setDescription('Assembles the executable.')
	executable "${projectDir}/etc/exewrap.exe"
	args "-A", "x64",
		"-g",
		"-t", "17",
		"-a", "-Xmx1024m -Djdk.tls.client.protocols=TLSv1,TLSv1.1,TLSv1.2",
		"-L", "lib",
		"-e", "SHARED",
		"-j", "${jar.archiveFile.get()}",
		"-i", "etc/ico/app.ico",
		"-o", "${buildDir}/package/${archivesBaseName}.exe",
		"-d", "Catalpa",
		"-p", "Catalpa",
		"-c", "(C) 2019-2021 HIRUKAWA Ryo",
		"-V", "${version}",
		"-v", "${version}"
}
